apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'

def integrationTests = false

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    defaultConfig {
        applicationId "com.example.reactivemvp"
        minSdkVersion 23
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        //include the integration test folder
        test.java.srcDirs +=  file('src/integrationTest/java')
    }

    testOptions {
        unitTests.all {
            useJUnit()
            if (integrationTests.toBoolean()) {
                println "Integration Tests Only for " + it.name
                options {
                    excludeCategories 'com.example.reactivemvp.categories.UnitTest'
                }
            } else {
                println "Unit Tests Only for " + it.name
                options {
                    excludeCategories 'com.example.reactivemvp.categories.IntegrationTest'
                }
            }
        }
    }
}

/**
 * Gradle task to run only integration tests that were flagged with specific category.
 */
task integrationTest(
        type: Test,
        description: 'Run integration tests only. Pass in \'-Pintegration=true\'',
        dependsOn: ['testDebugUnitTest', 'clean'] ) {

    //Here for task completion, not actually used since sub task of testDebugUnitTest
    testClassesDir = file("src/integrationTest/java/");
    classpath = files("$System.env.ANDROID_HOME/sources/android-18")

    //
    //Turn on integration testing when argument exists and is true
    //
    if (project.hasProperty('integration')) {
        println integration
        if (integration == 'true') {
            integrationTests = true
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile 'com.android.support:appcompat-v7:' + rootProject.ext.supportV7Version
    compile 'com.android.support:support-annotations:' + rootProject.ext.supportVersion
    compile 'com.android.support:design:' + rootProject.ext.supportVersion
    compile 'com.android.support:recyclerview-v7:' + rootProject.ext.supportV7Version
    compile 'com.jakewharton:butterknife:' + rootProject.ext.butterknifeVersion
    compile 'com.jakewharton.timber:timber:4.1.2'

    //Dagger
    compile 'com.google.dagger:dagger:' + rootProject.ext.daggerVersion
    apt 'com.google.dagger:dagger-compiler:' + rootProject.ext.daggerVersion
    provided 'javax.annotation:jsr250-api:1.0'

    //okhttp
    compile 'com.squareup.okhttp3:okhttp:' + rootProject.ext.okhttpVersion
    compile 'com.squareup.okhttp3:okhttp-urlconnection:' + rootProject.ext.okhttpVersion
    compile 'com.squareup.okhttp3:logging-interceptor:' + rootProject.ext.okhttpVersion
    compile 'com.squareup.okio:okio:' + rootProject.ext.okioVersion

    //Retrofit
    compile('com.squareup.retrofit2:retrofit:' + rootProject.ext.retrofitVersion) {
        //exclude Retrofit's Okhttp peer-dependency module and define your own module import
        exclude module: 'okhttp'
    }
    compile 'com.squareup.retrofit2:converter-gson:' + rootProject.ext.retrofitVersion
    compile 'com.squareup.retrofit2:adapter-rxjava:' + rootProject.ext.retrofitVersion

    //Rx
    compile 'io.reactivex:rxandroid:' + rootProject.ext.reactiveXAndroidVersion
    compile 'io.reactivex:rxjava:' + rootProject.ext.reactiveXVersion

    //Unit Test
    testCompile 'junit:junit:' + rootProject.ext.junitVersion
    testCompile 'org.apache.maven:maven-ant-tasks:2.1.3'
    testCompile 'org.robolectric:robolectric:' + rootProject.ext.robolectricVersion
    testCompile 'org.robolectric:shadows-support-v4:3.0'
    testCompile 'org.mockito:mockito-core:' + rootProject.ext.mockitoVersion
    testCompile 'com.android.support:support-annotations:' + rootProject.ext.supportVersion
    testCompile 'org.hamcrest:hamcrest-all:1.3'
    testCompile 'org.assertj:assertj-core:1.7.1'
    testCompile('com.squareup.assertj:assertj-android:1.1.1') {
        exclude module: 'support-annotations'
    }
}
